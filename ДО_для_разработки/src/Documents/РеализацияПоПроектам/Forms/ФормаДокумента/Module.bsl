
&НаКлиенте
Процедура ПериодПриИзменении(Элемент)
	Объект.Период = НачалоМесяца(Объект.Период);
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьРасходыНаСервере()
	
	СтатьяВзаиморасчетСОПП = Справочники.пби_СтатьиОборотов.НайтиПоКоду("000000015");
	СтатьяФОТ = Справочники.пби_СтатьиОборотов.НайтиПоКоду("000000022");
	СтатьяАдминистративныеРасходы = Справочники.пби_СтатьиОборотов.НайтиПоКоду("000000026");
	СтатьяРеализация = Справочники.пби_СтатьиОборотов.НайтиПоКоду("000000003");
	СтатьяКоличествоСотрудников = Справочники.пби_СтатьиОборотов.НайтиПоКоду("000000025");
	СтатьяЕСН = Справочники.пби_СтатьиОборотов.НайтиПоКоду("000000024");
	ПоказательСтавка = Справочники.пби_Показатели.НайтиПоКоду("000000007");
	ПоказательОклад = Справочники.пби_Показатели.НайтиПоКоду("000000008");
	ПоказательАдминистративныеРасходы = Справочники.пби_Показатели.НайтиПоКоду("000000002");
	ПоказательПроцентОПП = Справочники.пби_Показатели.НайтиПоКоду("000000004");
	ПоказательЕСН = Справочники.пби_Показатели.НайтиПоКоду("000000001"); 
	  
		
	ТЗ_Статьи = Объект.Проекты.Выгрузить();
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТЗ_Статьи.Статья
	|ПОМЕСТИТЬ ТЗ_Статьи
	|ИЗ
	|	&ТЗ_Статьи КАК ТЗ_Статьи
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	пби_ГрейдыСрезПоследних.Сотрудник,
	|	МАКСИМУМ(пби_ГрейдыСрезПоследних.Ставка) КАК Ставка,
	|	пби_ГрейдыСрезПоследних.Показатель
	|ПОМЕСТИТЬ Грейды
	|ИЗ
	|	РегистрСведений.пби_Грейды.СрезПоследних(&Период, ) КАК пби_ГрейдыСрезПоследних
	|
	|СГРУППИРОВАТЬ ПО
	|	пби_ГрейдыСрезПоследних.Сотрудник,
	|	пби_ГрейдыСрезПоследних.Показатель
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ФактическиеТрудозатраты.Пользователь,
	|	СУММА(ФактическиеТрудозатраты.Длительность/3600) КАК Длительность
	|ПОМЕСТИТЬ РабочиеЧасы
	|ИЗ
	|	РегистрСведений.ФактическиеТрудозатраты КАК ФактическиеТрудозатраты
	|ГДЕ
	|	ФактическиеТрудозатраты.Подразделение = &Подразделение
	|	И ФактическиеТрудозатраты.ДатаДобавления >= &Период
	|	И ФактическиеТрудозатраты.ДатаДобавления <= КОНЕЦПЕРИОДА(&Период, МЕСЯЦ)
	|
	|СГРУППИРОВАТЬ ПО
	|	ФактическиеТрудозатраты.Пользователь
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	пби_ПоказателиДляРасчетовСрезПоследних.Показатель,
	|	пби_ПоказателиДляРасчетовСрезПоследних.Значение
	|ПОМЕСТИТЬ Показатели
	|ИЗ
	|	РегистрСведений.пби_ПоказателиДляРасчетов.СрезПоследних(&Период, ) КАК пби_ПоказателиДляРасчетовСрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	РеализацииПоПроектамОбороты.СуммаОборот
	|ПОМЕСТИТЬ СуммаРеализации
	|ИЗ
	|	РегистрНакопления.РеализацииПоПроектам.Обороты(
	|			&Период,
	|			КОНЕЦПЕРИОДА(&Период, МЕСЯЦ),
	|			Месяц,
	|			Сценарий = &Сценарий
	|				И Статья В ИЕРАРХИИ (&СтатьяРеализация)
	|				И Подразделение = &Подразделение) КАК РеализацииПоПроектамОбороты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Пользователи.Ссылка) КАК Количество,
	|	&СтатьяКоличествоСотрудников
	|ПОМЕСТИТЬ КоличествоСотрудников
	|ИЗ
	|	Справочник.Пользователи КАК Пользователи
	|ГДЕ
	|	Пользователи.Подразделение = &Подразделение
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(ВЫБОР
	|			КОГДА Грейды.Показатель = &ПоказательСтавка
	|				ТОГДА РабочиеЧасы.Длительность * Грейды.Ставка
	|			КОГДА Грейды.Показатель = &ПоказательОклад
	|				ТОГДА Грейды.Ставка
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК Сумма,
	|	&СтатьяФОТ КАК Статья
	|ПОМЕСТИТЬ ФОТ
	|ИЗ
	|	РабочиеЧасы КАК РабочиеЧасы
	|		ЛЕВОЕ СОЕДИНЕНИЕ Грейды КАК Грейды
	|		ПО РабочиеЧасы.Пользователь = Грейды.Сотрудник
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	&СтатьяАдминистративныеРасходы,
	|	СУММА(КоличествоСотрудников.Количество * Показатели.Значение) КАК Сумма
	|ПОМЕСТИТЬ АдминистративныеРасходы
	|ИЗ
	|	КоличествоСотрудников КАК КоличествоСотрудников
	|		ПОЛНОЕ СОЕДИНЕНИЕ Показатели КАК Показатели
	|		ПО (ИСТИНА)
	|ГДЕ
	|	Показатели.Показатель = &ПоказательАдминистративныеРасходы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	&СтатьяЕСН,
	|	СУММА(Показатели.Значение / 100 * ФОТ.Сумма) КАК Сумма
	|ПОМЕСТИТЬ ЕСН
	|ИЗ
	|	ФОТ КАК ФОТ
	|		ПОЛНОЕ СОЕДИНЕНИЕ Показатели КАК Показатели
	|		ПО (ИСТИНА)
	|ГДЕ
	|	Показатели.Показатель = &ПоказательЕСН
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Показатели.Значение / 100 * СуммаРеализации.СуммаОборот КАК Сумма,
	|	&СтатьяВзаиморасчетСОПП
	|ПОМЕСТИТЬ ВзаиморасчетСОПП
	|ИЗ
	|	СуммаРеализации КАК СуммаРеализации
	|		ПОЛНОЕ СОЕДИНЕНИЕ Показатели КАК Показатели
	|		ПО (ИСТИНА)
	|ГДЕ
	|	Показатели.Показатель = &ПоказательПроцентОПП
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(КоличествоСотрудников.Количество) КАК Сумма,
	|	КоличествоСотрудников.СтатьяКоличествоСотрудников КАК Статья,
	|	NULL КАК Проект,
	|	NULL КАК ПроектнаяЗадача
	|ПОМЕСТИТЬ ИтогВычислений
	|ИЗ
	|	КоличествоСотрудников КАК КоличествоСотрудников
	|
	|СГРУППИРОВАТЬ ПО
	|	КоличествоСотрудников.СтатьяКоличествоСотрудников
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СУММА(ФОТ.Сумма),
	|	ФОТ.Статья,
	|	NULL,
	|	NULL
	|ИЗ
	|	ФОТ КАК ФОТ
	|
	|СГРУППИРОВАТЬ ПО
	|	ФОТ.Статья
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СУММА(АдминистративныеРасходы.Сумма),
	|	АдминистративныеРасходы.СтатьяАдминистративныеРасходы,
	|	NULL,
	|	NULL
	|ИЗ
	|	АдминистративныеРасходы КАК АдминистративныеРасходы
	|
	|СГРУППИРОВАТЬ ПО
	|	АдминистративныеРасходы.СтатьяАдминистративныеРасходы
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СУММА(ВзаиморасчетСОПП.Сумма),
	|	ВзаиморасчетСОПП.СтатьяВзаиморасчетСОПП,
	|	NULL,
	|	NULL
	|ИЗ
	|	ВзаиморасчетСОПП КАК ВзаиморасчетСОПП
	|
	|СГРУППИРОВАТЬ ПО
	|	ВзаиморасчетСОПП.СтатьяВзаиморасчетСОПП
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЕСН.Сумма,
	|	ЕСН.СтатьяЕСН,
	|	NULL,
	|	NULL
	|ИЗ
	|	ЕСН КАК ЕСН
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТЗ_Статьи.Статья,
	|	ИтогВычислений.Проект,
	|	ИтогВычислений.ПроектнаяЗадача,
	|	ИтогВычислений.Сумма
	|ИЗ
	|	ТЗ_Статьи КАК ТЗ_Статьи
	|		ЛЕВОЕ СОЕДИНЕНИЕ ИтогВычислений КАК ИтогВычислений
	|		ПО ТЗ_Статьи.Статья = ИтогВычислений.Статья";
	Запрос.МенеджерВременныхТаблиц = новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ТЗ_Статьи", ТЗ_Статьи);
	Запрос.УстановитьПараметр("Период", Объект.Период);
	Запрос.УстановитьПараметр("Сценарий", Объект.Сценарий);
	Запрос.УстановитьПараметр("Подразделение", Объект.Подразделение);
	Запрос.УстановитьПараметр("СтатьяВзаиморасчетСОПП", СтатьяВзаиморасчетСОПП);
	Запрос.УстановитьПараметр("ПоказательАдминистративныеРасходы", ПоказательАдминистративныеРасходы);
	Запрос.УстановитьПараметр("СтатьяАдминистративныеРасходы", СтатьяАдминистративныеРасходы);
	Запрос.УстановитьПараметр("СтатьяРеализация", СтатьяРеализация);
	Запрос.УстановитьПараметр("СтатьяКоличествоСотрудников", СтатьяКоличествоСотрудников);
	Запрос.УстановитьПараметр("СтатьяЕСН",СтатьяЕСН);
	Запрос.УстановитьПараметр("ПоказательСтавка", ПоказательСтавка);
	Запрос.УстановитьПараметр("ПоказательОклад", ПоказательОклад);
	Запрос.УстановитьПараметр("ПоказательПроцентОПП", ПоказательПроцентОПП);
	Запрос.УстановитьПараметр("ПоказательЕСН", ПоказательЕСН);
	Запрос.УстановитьПараметр("СтатьяФОТ", СтатьяФОТ);    	
	РезультатЗапроса = Запрос.Выполнить();
	Рез = РезультатЗапроса.Выгрузить();
	Объект.Проекты.Загрузить(Рез);
			
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьРасходы(Команда)
	Если Объект.Проекты.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	ЗаполнитьРасходыНаСервере(); 	
		
КонецПроцедуры
